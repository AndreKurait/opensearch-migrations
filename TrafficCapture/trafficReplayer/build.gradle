/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

plugins {
    id 'org.opensearch.migrations.java-application-conventions'
    id 'io.freefair.lombok'
    id 'java-test-fixtures'
}

repositories {
    mavenCentral()
}

dependencies {
    // api project(":trafficCaptureDependencies")

    implementation project(':captureProtobufs')
    implementation project(':coreUtilities')
    implementation project(':replayerPlugins:jsonMessageTransformers:jsonMessageTransformerInterface')
    runtimeOnly project(':replayerPlugins:jsonMessageTransformers:jsonJMESPathMessageTransformerProvider')
    runtimeOnly project(':replayerPlugins:jsonMessageTransformers:jsonJoltMessageTransformerProvider')
    runtimeOnly project(':replayerPlugins:jsonMessageTransformers:openSearch23PlusTargetTransformerProvider')

    implementation group: 'com.beust', name: 'jcommander'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    implementation group: 'com.google.guava', name: 'guava'
    implementation group: 'com.google.protobuf', name: 'protobuf-java'
    implementation group: 'io.github.resilience4j', name: 'resilience4j-ratelimiter'
    implementation group: 'io.github.resilience4j', name: 'resilience4j-retry'
    implementation group: 'io.netty', name: 'netty-all'
    implementation group: 'org.apache.kafka', name: 'kafka-clients'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl'
    implementation group: 'org.slf4j', name: 'slf4j-api'
    implementation group: 'software.amazon.awssdk', name: 'arns'
    implementation group: 'software.amazon.awssdk', name: 'auth'
    implementation group: 'software.amazon.awssdk', name: 'sdk-core'
    implementation group: 'software.amazon.awssdk', name: 'secretsmanager'
    implementation group: 'software.amazon.msk', name: 'aws-msk-iam-auth'
    implementation 'org.apache.commons:commons-compress'

    testFixturesImplementation project(':replayerPlugins:jsonMessageTransformers:jsonMessageTransformerInterface')
    testFixturesImplementation project(':captureOffloader')
    testFixturesImplementation testFixtures(project(':captureOffloader'))
    testFixturesImplementation project(':captureProtobufs')
    testFixturesImplementation project(':coreUtilities')
    testFixturesImplementation testFixtures(project(path: ':coreUtilities'))
    testFixturesImplementation testFixtures(project(path: ':testUtilities'))

    testFixturesImplementation group: 'org.slf4j', name: 'slf4j-api'
    testFixturesImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.0'
    testFixturesImplementation group: 'io.netty', name: 'netty-all', version: versions.netty
    testFixturesImplementation group: 'org.junit.jupiter', name:'junit-jupiter-api', version:'5.9.3'

    testImplementation testFixtures(project(path: ':testUtilities'))
    testImplementation testFixtures(project(path: ':coreUtilities'))
    testImplementation project(':replayerPlugins:jsonMessageTransformers:jsonJMESPathMessageTransformerProvider')
    testImplementation project(':replayerPlugins:jsonMessageTransformers:jsonJoltMessageTransformerProvider')
    testImplementation project(':replayerPlugins:jsonMessageTransformers:openSearch23PlusTargetTransformerProvider')

    testImplementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'
    testImplementation group: 'org.junit.jupiter', name:'junit-jupiter-api', version:'5.x.x'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.19.7'
    testImplementation group: 'org.testcontainers', name: 'kafka', version: '1.19.7'
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.19.7'

    testImplementation group: 'org.mockito', name:'mockito-core', version: versions.mockito
    testImplementation group: 'org.mockito', name:'mockito-junit-jupiter', version: versions.mockito
    testRuntimeOnly group:'org.junit.jupiter', name:'junit-jupiter-engine', version:'5.x.x'

    constraints {
        testImplementation('org.apache.commons:commons-compress:1.26.0') {
            because 'mend security issue'
        }
        implementation('io.netty:netty-codec-http:4.1.108.Final') {
            because 'mend security issue'
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-text') {
            def targetVersion = '1.10.0'
            if (CommonUtils.wasRequestedVersionReleasedBeforeTargetVersion(details.requested.version, targetVersion)) {
                details.useVersion targetVersion
            }
        }
        if (details.requested.group == 'org.apache.bcel' && details.requested.name == 'bcel') {
            def targetVersion = '6.7.0'
            if (CommonUtils.wasRequestedVersionReleasedBeforeTargetVersion(details.requested.version, targetVersion)) {
                details.useVersion targetVersion
            }
        }
        if (details.requested.group == 'io.netty') {
            details.useVersion versions.netty
        }
    }
}

application {
    mainClass = 'org.opensearch.migrations.replay.TrafficReplayer'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}
