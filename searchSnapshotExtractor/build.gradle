/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

plugins {
    id 'org.opensearch.migrations.java-library-conventions'
    id 'io.freefair.lombok'
    id 'com.gradleup.shadow'
}

configurations {
    lucene5
    lucene6
    lucene7
    lucene9
}

task shadowLucene5(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene5]
    archiveClassifier.set('lucene5-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene5.org.apache.lucene'
}

task shadowLucene6(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene6]
    archiveClassifier.set('lucene6-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene6.org.apache.lucene'
}

task shadowLucene7(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    configurations = [project.configurations.lucene7]
    archiveClassifier.set('lucene7-shadow')
    relocate 'org.apache.lucene', 'shadow.lucene7.org.apache.lucene'
}

task shadowLucene9(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    archiveClassifier.set('lucene9-shadow')
    configurations = [project.configurations.lucene9]
    relocate 'org.apache.lucene', 'shadow.lucene9.org.apache.lucene'
}

tasks.register('prepareShadowJars') {
    dependsOn shadowLucene5, shadowLucene6, shadowLucene7, shadowLucene9
}

tasks.named('build') {
    dependsOn prepareShadowJars
}

tasks.named('test') {
    dependsOn prepareShadowJars
}

dependencies {
    annotationProcessor libs.lombok

    implementation project(':coreUtilities')

    testImplementation libs.log4j.api
    testImplementation libs.log4j.core
    testImplementation libs.log4j.slf4j2.impl

    implementation libs.jackson.databind
    implementation libs.jackson.annotations
    implementation libs.jackson.core
    implementation libs.jackson.dataformat.smile

    // AWS S3 dependencies for S3BlobSource
    implementation libs.aws.s3
    implementation libs.aws.s3.transfer.manager
    implementation libs.aws.crt

    // SLF4J for logging
    implementation libs.slf4j.api

    lucene5 libs.lucene.v5.core
    lucene5 libs.lucene.v5.backward.codecs

    lucene6 libs.lucene.v6.core
    lucene6 libs.lucene.v6.backward.codecs

    lucene7 libs.lucene.v7.core
    lucene7 libs.lucene.v7.backward.codecs

    lucene9 libs.lucene.v9.core
    lucene9 libs.lucene.v9.analysis.common
    lucene9 libs.lucene.v9.backward.codecs

    implementation shadowLucene5.outputs.files
    implementation shadowLucene6.outputs.files
    implementation shadowLucene7.outputs.files
    implementation shadowLucene9.outputs.files

    testImplementation shadowLucene5.outputs.files
    testImplementation shadowLucene6.outputs.files
    testImplementation shadowLucene7.outputs.files
    testImplementation shadowLucene9.outputs.files

    testFixturesImplementation shadowLucene5.outputs.files
    testFixturesImplementation shadowLucene6.outputs.files
    testFixturesImplementation shadowLucene7.outputs.files
    testFixturesImplementation shadowLucene9.outputs.files

    // Test dependencies
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.mockito.core
    testImplementation libs.mockito.junit.jupiter
    testRuntimeOnly libs.junit.jupiter.engine
}
